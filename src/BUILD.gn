declare_args() {
  # Sets -DV8JSI_ENABLE_INSPECTOR
  v8jsi_enable_inspector = true
}

target("shared_library", "v8jsi") {
  sources = [
    "jsi/decorator.h",
    "jsi/instrumentation.h",
    "jsi/jsi-inl.h",
    "jsi/jsi.cpp",
    "jsi/jsi.h",
    "jsi/JSIDynamic.h",
    "jsi/jsilib-windows.cpp",
    "jsi/jsilib.h",
    "jsi/threadsafe.h",
    "napi/env-inl.h",
    "napi/js_native_api_v8_internals.h",
    "napi/js_native_api_v8.cc",
    "napi/js_native_api_v8.h",
    "napi/js_native_ext_api_v8.cpp",
    "napi/util-inl.h",
    "public/compat.h",
    "public/js_native_api_types.h",
    "public/js_native_api.h",
    "public/js_native_ext_api.h",
    "public/NapiJsiRuntime.cpp",
    "public/ScriptStore.h",
    "public/V8JsiRuntime.h",
    "V8JsiRuntime_impl.h",
    "V8JsiRuntime.cpp",
    "IsolateData.h",
    "MurmurHash.h",
    "MurmurHash.cpp",
  ]

  if (is_win) {
    sources += [
      "version_gen.rc",
      "etw/tracing.h",
      "etw/tracing.cpp"
    ]

    if (v8jsi_enable_inspector) {
      sources += [
        "inspector/inspector_agent.cpp",
        "inspector/inspector_agent.h",
        "inspector/inspector_socket.cpp",
        "inspector/inspector_socket.h",
        "inspector/inspector_socket_server.cpp",
        "inspector/inspector_socket_server.h",
        "inspector/inspector_tcp.cpp",
        "inspector/inspector_tcp.h",
        "inspector/inspector_utils.cpp",
        "inspector/inspector_utils.h",
        "inspector/llhttp.c",
        "inspector/llhttp.h",
        "inspector/llhttp_api.c",
        "inspector/llhttp_http.c"
      ]
    }

    ldflags = [ "/OPT:REF", "/INCREMENTAL:NO" ]

    if (!is_clang) {
      ldflags += [ "/PDBSTRIPPED:v8jsi_stripped.dll.pdb", "/SOURCELINK:" + rebase_path("//jsi/source_link_gen.json") ]
    }
  } else if (!is_mac) {
    ldflags = [ "-Wl,--allow-multiple-definition" ]
    ldflags += [ "-Wl,--version-script=" + rebase_path("//jsi/makev8jsi.lst") ] 
  }

  include_dirs = [ ".", "../include", "jsi", "public", getenv("ASIO_ROOT") ]

  configs += [ "//:toolchain", "//:internal_config_base", "//build/config/compiler:exceptions" ]
  configs -= [ "//build/config/compiler:no_exceptions" ]

  if (is_win) {
    configs -= [ "//build/config/compiler:cet_shadow_stack" ]
    configs -= [ "//build/config/win:default_incremental_linking" ]
    configs += [ "//build/config/win:win_msvc_cfg" ]
  }

  deps = [
    "//:v8_headers",
    "//:v8_monolith"
  ]

  defines = [ "BUILDING_V8JSI_SHARED" ]

  if (is_win) {
    libs = [
      "winmm.lib",
      "bcrypt.lib",
      "shlwapi.lib",
    ]

    if (target_os != "winuwp") {
      libs += [ "dbghelp.lib" ]
    }
  }

  if (v8jsi_enable_inspector) {
    defines += [ "V8JSI_ENABLE_INSPECTOR" ]
  }
}

# copy test JS files
copy("napi_test_js_files") {
  sources = [
    "napi/test/js-native-api/2_function_arguments/test.js",
    "napi/test/js-native-api/3_callbacks/test.js",
    "napi/test/js-native-api/4_object_factory/test.js",
    "napi/test/js-native-api/5_function_factory/test.js",
    "napi/test/js-native-api/6_object_wrap/test.js",
    "napi/test/js-native-api/7_factory_wrap/test.js",
    "napi/test/js-native-api/8_passing_wrapped/test.js",
    "napi/test/js-native-api/common/assert.js",
    "napi/test/js-native-api/common/common.js",
    "napi/test/js-native-api/test_array/test.js",
    "napi/test/js-native-api/test_bigint/test.js",
    "napi/test/js-native-api/test_constructor/test.js",
    "napi/test/js-native-api/test_constructor/test2.js",
    "napi/test/js-native-api/test_conversions/test.js",
    "napi/test/js-native-api/test_dataview/test.js",
    "napi/test/js-native-api/test_date/test.js",
    "napi/test/js-native-api/test_error/test.js",
    "napi/test/js-native-api/test_exception/test.js",
    "napi/test/js-native-api/test_exception/testFinalizerException.js",
    "napi/test/js-native-api/test_function/test.js",
    "napi/test/js-native-api/test_general/test.js",
    "napi/test/js-native-api/test_general/testEnvCleanup.js",
    "napi/test/js-native-api/test_general/testFinalizer.js",
    "napi/test/js-native-api/test_general/testGlobals.js",
    "napi/test/js-native-api/test_general/testInstanceOf.js",
    "napi/test/js-native-api/test_general/testNapiRun.js",
    "napi/test/js-native-api/test_general/testNapiStatus.js",
    "napi/test/js-native-api/test_handle_scope/test.js",
    "napi/test/js-native-api/test_instance_data/test.js",
    "napi/test/js-native-api/test_new_target/test.js",
    "napi/test/js-native-api/test_number/test.js",
    "napi/test/js-native-api/test_object/test.js",
    "napi/test/js-native-api/test_object/test_null.js",
    "napi/test/js-native-api/test_promise/test.js",
    "napi/test/js-native-api/test_properties/test.js",
    "napi/test/js-native-api/test_reference/test.js",
    "napi/test/js-native-api/test_reference_double_free/test.js",
    "napi/test/js-native-api/test_string/test.js",
    "napi/test/js-native-api/test_symbol/test1.js",
    "napi/test/js-native-api/test_symbol/test2.js",
    "napi/test/js-native-api/test_symbol/test3.js",
    "napi/test/js-native-api/test_typedarray/test.js",
  ]
  outputs = [ "$root_out_dir/{{source}}" ]
}

target("executable", "jsitests") {
  testonly = true

  deps = [
    ":v8jsi",
    ":napi_test_js_files",
    "//build/win:default_exe_manifest",
    "//testing/gtest",
  ]

  configs += [ "//:toolchain", "//:internal_config_base", "//build/config/compiler:exceptions", "//build/config/compiler:rtti" ]
  configs -= [ "//build/config/compiler:no_exceptions", "//build/config/compiler:no_rtti" ]

  if (is_win) {
    # Import NAPI functions and (-FC) use full path of source code files in diagnostics.
    cflags = [ "-DNAPI_EXTERN=__declspec(dllimport)", "-FC" ]
  }

  include_dirs = [ ".", "jsi", "napi", "napi/test", "public" ]

  sources = [
    "jsi/decorator.h",
    "jsi/instrumentation.h",
    "jsi/jsi-inl.h",
    "jsi/jsi.cpp",
    "jsi/jsi.h",
    "jsi/JSIDynamic.h",
    "jsi/jsilib-windows.cpp",
    "jsi/jsilib.h",
    "jsi/threadsafe.h",
    "jsi/test/testlib.h",
    "jsi/test/testlib.cpp",
    "napi/test/napitest_v8.cpp",
    "napi/test/napitest.cpp",
    "napi/test/napitest.h",
    "napi/test/test_2_function_arguments.cpp",
    "napi/test/test_3_callbacks.cpp",
    "napi/test/test_4_object_factory.cpp",
    "napi/test/test_5_function_factory.cpp",
    "napi/test/test_6_object_wrap.cpp",
    "napi/test/test_7_factory_wrap.cpp",
    "napi/test/test_8_passing_wrapped.cpp",
    "napi/test/test_array.cpp",
    "napi/test/test_assert.cpp",
    "napi/test/test_bigint.cpp",
    "napi/test/test_constructor.cpp",
    "napi/test/test_conversions.cpp",
    "napi/test/test_dataview.cpp",
    "napi/test/test_date.cpp",
    "napi/test/test_error.cpp",
    "napi/test/test_exception.cpp",
    "napi/test/test_ext.cpp",
    "napi/test/test_function.cpp",
    "napi/test/test_general.cpp",
    "napi/test/test_handle_scope.cpp",
    "napi/test/test_instance_data.cpp",
    "napi/test/test_new_target.cpp",
    "napi/test/test_number.cpp",
    "napi/test/test_object.cpp",
    "napi/test/test_promise.cpp",
    "napi/test/test_properties.cpp",
    "napi/test/test_reference_double_free.cpp",
    "napi/test/test_reference.cpp",
    "napi/test/test_string.cpp",
    "napi/test/test_symbol.cpp",
    "napi/test/test_typedarray.cpp",
    "public/compat.h",
    "public/NapiJsiRuntime.cpp",
    "public/NapiJsiRuntime.h",
    "testmain.cpp"
  ]
}
