#TODO: use this template to preprocess version information
#import("//build/util/process_version.gni")
#process_version("v8jsi_version") {
#  template_file = "version.rc"
#  sources = []
#  output = "$target_gen_dir/v8jsi_version.rc"
#}

target("shared_library", "v8jsi") {
  sources = [
    "jsi/decorator.h",
    "jsi/instrumentation.h",
    "jsi/jsi-inl.h",
    "jsi/jsi.cpp",
    "jsi/jsi.h",
    "jsi/JSIDynamic.h",
    "jsi/jsilib-windows.cpp",
    "jsi/jsilib.h",
    "jsi/threadsafe.h",
    "napi/env-inl.h",
    "napi/js_native_api_v8_internals.h",
    "napi/js_native_api_v8.cc",
    "napi/js_native_api_v8.h",
    "napi/js_native_ext_api_v8.cpp",
    "napi/util-inl.h",
    "public/js_native_api_types.h",
    "public/js_native_api.h",
    "public/js_native_ext_api.h",
    "public/ScriptStore.h",
    "public/V8JsiRuntime.h",
    "V8JsiRuntime_impl.h",
    "V8JsiRuntime.cpp",
    "V8Platform.cpp",
    "V8Platform.h",
  ]

  cflags = [ "-DBOOST_ASIO_STANDALONE", "-DBOOST_ASIO_HEADER_ONLY", "-DUSE_DEFAULT_PLATFORM" ]

  if (is_win) {
    sources += [
      "version_gen.rc",
      "etw/tracing.h",
      "etw/tracing.cpp",
      "inspector/inspector_agent.cpp",
      "inspector/inspector_agent.h",
      "inspector/inspector_socket.cpp",
      "inspector/inspector_socket.h",
      "inspector/inspector_socket_server.cpp",
      "inspector/inspector_socket_server.h",
      "inspector/inspector_tcp.cpp",
      "inspector/inspector_tcp.h",
      "inspector/inspector_utils.cpp",
      "inspector/inspector_utils.h",
      "inspector/llhttp.c",
      "inspector/llhttp.h",
      "inspector/llhttp_api.c",
      "inspector/llhttp_http.c"
    ]

    ldflags = [ "/OPT:REF", "/INCREMENTAL:NO" ]

    if (!is_clang) {
      ldflags += [ "/PDBSTRIPPED:v8jsi_stripped.dll.pdb" ]
    }
  }

  include_dirs = [ ".", "../include", "jsi", "public", getenv("BOOST_ROOT") ]

  configs += [ "//:internal_config_base", "//build/config/compiler:exceptions" ]
  configs -= [ "//build/config/compiler:no_exceptions" ]
  configs -= [ "//build/config/win:default_incremental_linking" ]

  if (is_win) {
    configs += [ "//build/config/win:win_msvc_cfg" ]
  }

  deps = [
    "//:v8_headers",
    "//:v8_monolith"
  ]

  if (is_win) {
    defines = [ "BUILDING_V8_SHARED", "BOOST_ALL_NO_LIB" ]
    libs = [
      "winmm.lib",
      "bcrypt.lib",
      "shlwapi.lib",
    ]

    if (target_os != "winuwp") {
      libs += [ "dbghelp.lib" ]
    }
  }
}

target("executable", "jsitests") {
  testonly = true

  deps = [
    ":v8jsi",
    "//build/win:default_exe_manifest",
    "//testing/gtest",
  ] 

  configs += [ "//:internal_config_base", "//build/config/compiler:exceptions", "//build/config/compiler:rtti" ]
  configs -= [ "//build/config/compiler:no_exceptions", "//build/config/compiler:no_rtti" ]

  cflags = [ "-DNAPI_EXTERN=__declspec(dllimport)", "-FC" ]

  include_dirs = [ ".", "jsi", "napi", "napi/test", "public" ]

  sources = [
    "jsi/decorator.h",
    "jsi/instrumentation.h",
    "jsi/jsi-inl.h",
    "jsi/jsi.cpp",
    "jsi/jsi.h",
    "jsi/JSIDynamic.h",
    "jsi/jsilib-windows.cpp",
    "jsi/jsilib.h",
    "jsi/threadsafe.h",
    "jsi/test/testlib.h",
    "jsi/test/testlib.cpp",
    "napi/test/lib/assert_js.cpp",
    "napi/test/lib/common_js.cpp",
    "napi/test/lib/modules.h",
    "napi/test/napitest_v8.cpp",
    "napi/test/napitest.cpp",
    "napi/test/napitest.h",
    "napi/test/test_array.cpp",
    "napi/test/test_assert.cpp",
    "napi/test/test_bigint.cpp",
    "napi/test/test_constructor.cpp",
    "napi/test/test_conversions.cpp",
    "napi/test/test_dataview.cpp",
    "napi/test/test_date.cpp",
    "napi/test/test_error.cpp",
    "napi/test/test_exception.cpp",
    "napi/test/test_ext.cpp",
    "napi/test/test_function.cpp",
    "napi/test/test_general.cpp",
    "napi/test/test_handle_scope.cpp",
    "napi/test/test_instance_data.cpp",
    "napi/test/test_new_target.cpp",
    "napi/test/test_number.cpp",
    "napi/test/test_object.cpp",
    "napi/test/test_promise.cpp",
    "napi/test/test_properties.cpp",
    "napi/test/test_reference_double_free.cpp",
    "napi/test/test_reference.cpp",
    "napi/test/test_string.cpp",
    "napi/test/test_symbol.cpp",
    "napi/test/test_typedarray.cpp",
    "testmain.cpp"
  ]
}
